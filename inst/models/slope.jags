model
{
	#### counts and overdispersion effects  ###### Link-Sauer model with nested observer-route effect
	for( k in 1 : ncounts )
	{
		log(lambda[k]) <- beta[strat[k]] * (year[k] - fixedyear) + eta*firstyr[k] + obs[strat[k],obser[k]] + strata[strat[k]] + yeareffect[strat[k],year[k]] + noise[k]
	 	noise[k] ~ dnorm(0, taunoise)
    #noise[k] ~ dt(0, taunoise, nu) #alternative t-distributed noise = heavy-tailed overdispersion

		count[k] ~ dpois(lambda[k])
		#----------------------------------#
		#fcount[k] ~ dpois(lambda[k])
		#err[k] <- pow(count[k]-lambda[k],2)/lambda[k]
		#ferr[k] <- pow(fcount[k]-lambda[k],2)/lambda[k]
		#fzero[k] <- equals(fcount[k],0)
		#loglik[k] <- logdensity.pois(count[k], lambda[k])
		#----------------------------------#
	}

	#nfzero <- sum(fzero[1:ncounts])
	#gof <- sum(err[1:ncounts])
	#fgof <- sum(ferr[1:ncounts])
	#diffgof <- gof-fgof
	#posdiff <- step(diffgof)

	#nu ~ dgamma(2, 0.1) #degrees of freedom (i.e., the heavy-tail component of the t-distribution), if nu is large (infinite) this is equivalent to the normal distribution noise
	taunoise ~ dgamma(0.001,0.001)
	sdnoise <- 1 / pow(taunoise, 0.5)
	sdobs <- 1 / pow(tauobs, 0.5)
	tauobs ~ dgamma(0.001,0.001)
	eta ~ dnorm( 0.0,0.01)
	STRATA ~ dnorm( 0.0,0.01)
	taustrata ~ dgamma(0.001,0.0001) #<- 1/pow(sdbeta,2)#
	sdstrata <- 1/pow(taustrata,0.5)#~ dunif(0.001,10)
	mulogtauyear ~ dnorm(0.0,0.5)
	taulogtauyear ~ dgamma(2,0.2)

	#---------------------------------------------------------#

	#----------------------------------#
	#### stratum effects  ######
	for( i in 1 : nstrata )
	{
		#### observer effects  ######

		for( o in 1 : nobservers[i] )
		{
			obs[i,o] ~ dnorm( 0.0,tauobs)
		}

		#### end observer effects  ######

		beta.p[i] ~ dnorm(0,taubeta)
		beta[i] <- BETA + beta.p[i]
		strata.p[i] ~ dnorm(0,taustrata)
		strata[i] <- STRATA + strata.p[i]
		#sdyear[i] <- 1/pow(tauyear[i],0.5)
		#tauyear[i] ~ dgamma(0.001,0.001) #

		#### stratum specific year effects  ######


		log(tauyear[i]) <- logtauyear[i]
		logtauyear[i] ~ dnorm(mulogtauyear,taulogtauyear)
		sdyear[i] <- 1/pow(tauyear[i],0.5)

		for( t in ymin : ymax )
		{
			yeareffect[i,t] ~ dnorm( 0.0, tauyear[i])
		}

		expstrata[i] <- exp(strata[i])
		overdisp[i] <- 1 + 1/(expstrata[i]*taunoise)
		#-------------------------------------------------#

	}# end s strata loop



	#### hierarchical trend effects  ######


	BETA ~ dnorm(0,0.01)
  taubeta ~ dgamma(1,0.0001) #
	### informative taubeta, parameterized so that:
	### mean of taubeta = ~10000
	### the mean of sdbeta =~ 0.02
	### there is <5% prob of sdbeta > 0.04 (i.e., there is a small, but non-zero probability that the SD of the stratum beta
	### terms is large enough to to encompass trends that vary from -8%/year to 8%/year with a continental trend = 0)
	### this informative prior should only be useful when there are few strata

	sdbeta <- 1/pow(taubeta,0.5)#



	#### summary statistics  ######
	sdn_ret <- 0.5*sdnoise*sdnoise
	sdobs_ret <- 0.5*sdobs*sdobs

	for( i in 1 : nstrata )
	{
		 sdyear_ret[i] <- 0.5*sdyear[i]*sdyear[i]

		for( t in ymin : ymax )
		{
			for(o in 1 : nobservers[i])
			{
				no[i,t,o] <- exp(strata[i] + beta[i]*(t-fixedyear) + yeareffect[i,t] + obs[i,o]+sdn_ret)
				no3[i,t,o] <- exp(strata[i] + beta[i]*(t-fixedyear) + sdyear_ret[i] + obs[i,o]+sdn_ret)

			}

			mn[i,t] <- mean(no[i,t,1 : nobservers[i]])
			mn3[i,t] <- mean(no3[i,t,1 : nobservers[i]])
			n[i,t] <- nonzeroweight[i]*(mn[i,t])
			n3[i,t] <- nonzeroweight[i]*(mn3[i,t])

			n2[i,t] <- nonzeroweight[i]*exp(strata[i] + beta[i]*(t-fixedyear) + yeareffect[i,t] + sdn_ret + sdobs_ret)
			n4[i,t] <- nonzeroweight[i]*exp(strata[i] + beta[i]*(t-fixedyear) + sdyear_ret[i] + sdn_ret + sdobs_ret)
		}
	}

	#-------------------------------------------------#
}
